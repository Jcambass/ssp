// https://www.khronos.org/opengl/wiki/OpenGL_Shading_Language
// https://registry.khronos.org/OpenGL-Refpages/gl4/index.php

#pragma language glsl3

extern int mode;
extern bool debugDraw;
extern int pattern[64];
extern vec4 white;
extern vec4 black;
extern vec4 transparent;
// Dither parameters
extern bool ditherEnabled = false;
extern float ditherAlpha = 1.0;

extern float threshold = 0.5;

// Bayer 8x8 matrix (values 0-63)
const float bayer8x8[64] = float[64](
   0.0, 32.0,  8.0, 40.0,  2.0, 34.0, 10.0, 42.0,
  48.0, 16.0, 56.0, 24.0, 50.0, 18.0, 58.0, 26.0,
  12.0, 44.0,  4.0, 36.0, 14.0, 46.0,  6.0, 38.0,
  60.0, 28.0, 52.0, 20.0, 62.0, 30.0, 54.0, 22.0,
   3.0, 35.0, 11.0, 43.0,  1.0, 33.0,  9.0, 41.0,
  51.0, 19.0, 59.0, 27.0, 49.0, 17.0, 57.0, 25.0,
  15.0, 47.0,  7.0, 39.0, 13.0, 45.0,  5.0, 37.0,
  63.0, 31.0, 55.0, 23.0, 61.0, 29.0, 53.0, 21.0
);

vec4 effect(vec4 color, Image tex, vec2 tex_coords, vec2 screen_coords)
{
  vec4 inputColor = Texel(tex, tex_coords);

  // Special pattern mode - handle early and without color normalization.
  if (mode == 8) {
    // this mode does not exist on PD - this is to implement playdate.graphics.setPattern()

    // Use mod() to get the position of the current pixel within the 8x8 pattern
    int x = int(mod(screen_coords.x, 8.0));
    int y = int(mod(screen_coords.y, 8.0));

    vec4 result;
    // Use "x" and "y" multiplied by "w" to index into the pattern array
    if (pattern[x + y * 8] == 1) {
      result = white;
    } else {
      result = black;
    }
    
    // Preserve alpha from the input color
    result.a = inputColor.a;
    return result;
  }

  // Return transparent if input alpha is zero
  if (inputColor.a == 0.0) {
    return transparent;
  }

  // Standard luminance coefficients (Rec. 709, matches sRGB perception)
  float luminance = dot(inputColor.rgb, vec3(0.2126, 0.7152, 0.0722));
  vec4 result = luminance > threshold ? white : black;
  
  // Apply dithering if enabled (before any other processing)
  if (ditherEnabled) {
    if (result.a == 0.0) {
      return transparent;
    }
    
    // Get position in 8x8 Bayer matrix
    int x = int(mod(screen_coords.x, 8.0));
    int y = int(mod(screen_coords.y, 8.0));
    int index = y * 8 + x;
    
    // Get threshold from Bayer matrix
    float threshold = bayer8x8[index] / 63.0;
    
    // If alpha is less than threshold, discard this pixel (transparent)
    if (ditherAlpha < threshold) {
      return transparent;
    }
    // Continue with normal color processing below
  }

  if (mode == 0)                         // ---------- "copy" (default)
  {    
    // just return normalized color
    return result;
  }
   else if (mode == 1)                  // ---------- "whiteTransparent"
  {
    if (result == white) {
      return transparent;
    } else {
      return black;
    }
  }
  else if (mode == 2)                 // ---------- "blackTransparent"
  {
    if (result == black) {
      return transparent;
    } else {
      return white;
    }
  }
  else if (mode == 3)                          // ---------- "fillWhite"
  {
    return white;
  }
  else if (mode == 4)                     // ---------- "fillBlack"
  {
   return black;
  }
  else if (mode == 5)                  // ---------- "xor"
  {
    // Not implemented
  }
  else if (mode == 6)                 // ---------- "nxor"
  {
    // Not implemented
  }
  else if (mode == 7)                  // ---------- "inverted"
  {
    if (result == white) {
      return black;
    } else {
      return white;
    }
  }

  return result; // Fallback (should not reach here)
}